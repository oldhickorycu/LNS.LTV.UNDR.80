[
 POWERON NAME:    LNS.LTV.UNDR.80
 CREATION DATE:   10/26/2021
 AUTHOR:          CONNOR BLAKENEY
 PURPOSE:         BATCH REPORT OF TYPE 1 AND 2 LOANS CLOSED WITHIN DATE RANGE WITH LTV UNDER 80%.
]

TARGET = LOAN

DEFINE
 STARTDATE   = DATE
 ENDDATE     = DATE
 LTVVAR      = RATE
 LAID        = CHARACTER
 COUNTER     = NUMBER
END

SETUP
 STARTDATE = DATEREAD("Enter Start Date")
 ENDDATE   = DATEREAD("Enter End Date")
END

SELECT
 (LOAN:TYPE = 1 OR LOAN:TYPE = 2) AND
 LOAN:OPENDATE >= STARTDATE AND
 LOAN:OPENDATE <= ENDDATE AND
 LOAN:CLOSEDATE = '--/--/--'
END

PRINT TITLE = "LOANS UNDER 80% LTV" REPORTCATEGORY="ALLEGRO"

 HEADER = "ACCOUNT #     NAME                  LN   APP  TYPE      LTV     CREDIT SCORE    LOAN BALANCE"

 LTVVAR = 0.000%
 LAID   = LOAN:APPLICATIONID

 FOR ACCOUNT ACCOUNT:NUMBER
  DO
     FOR EACH LOANAPP WITH LOANAPP:ID = LAID
       DO
          LTVVAR = LOANAPP:LOANTOVALUERATIO
       END 
  END

 IF LTVVAR <= 80.000% THEN
  DO
    COL = 001 ACCOUNT:NUMBER
    COL = 015 NAME:SHORTNAME
    COL = 037 LOAN:ID
    COL = 043 LAID
    COL = 049 LOAN:TYPE
    COL = 055 LEFT LTVVAR
    COL = 071 LOAN:CREDITSCORE
    COL = 090 LOAN:BALANCE
    NEWLINE
    COUNTER = COUNTER + 1
  END

END

TOTAL
 NEWLINE
 PRINT "TOTAL COUNT: "
 PRINT COUNTER
 NEWLINE
 PRINT STARTDATE
 PRINT " - "
 PRINT ENDDATE
END
 
